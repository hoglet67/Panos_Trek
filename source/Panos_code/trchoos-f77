      SUBROUTINE CHOOSE(FROZEN)
C
C       25-APR-79
C       USE THE SYSTEM SERVICE SYS$GETTIM FOR THE RANDOM NUMBER SEED.
C       THIS GREATLY REDUCES DEPENDENCE OF THE SEED ON TIME OF DAY.
C
      INCLUDE 'TREKCOM-F77'
C     COMMON/SCANBF/KEY,AITEM
      COMMON/SCANBF/KEY
      COMMON/SCANBF1/AITEM
      COMMON/PASS/PFLAG
      LOGICAL PFLAG
      INTEGER ISEED(2)
      LOGICAL FROZEN
      LOGICAL CROP,NOMORE
      DOUBLE PRECISION AITEM,REGULAR,TOURNAMENT,FROZN,SHORT,MEDIUM,
     1 LONG,NOVICE,FAIR,GOOD,EMERITUS,EXPERT,RHBLANK
      DOUBLE PRECISION TNUMBER
      EQUIVALENCE (AITEM,TNUMBER)
      EQUIVALENCE (AITEM,ISEED)
      DATA REGULAR,TOURNAMENT,FROZN/7HREGULAR,8HTOURNAME,6HFROZEN/
      DATA SHORT,MEDIUM,LONG/5HSHORT,6HMEDIUM,4HLONG/
      DATA NOVICE,FAIR,GOOD,EXPERT/6HNOVICE,4HFAIR,4HGOOD,6HEXPERT/
      DATA EMERITUS,RHBLANK/8HEMERITUS,1H /
C
      TNUMBER = 0.
      PASSWD = RHBLANK
      ALLDONE=0
      GAMEWON=0
C     CALL SYS$GETTIM(ISEED)
      CALL RANSET(0)
      IPHWHO=0
 5    FROZEN = .FALSE.
C--------ASK FOR PARAMETERS OF GAME, PREFERABLY ALL ON ONE LINE
      CALL PROMPT(
     +54HWOULD YOU LIKE A REGULAR, TOURNAMENT, OR FROZEN GAME?   ,54)
      CALL SCAN
      IF(CROP(AITEM,REGULAR)) GO TO 9
      IF(CROP(AITEM,TOURNAMENT)) GO TO 100
      IF(CROP(AITEM,FROZN)) GO TO 200
      GO TO 5
 9    SKILL=0
      LENGTH=0
      IF(NOMORE()) GO TO 20
 10   CALL SCAN
      IF(KEY .NE. IHALPHA) GO TO 20
C--------CHECK FOR DIFFERENT KINDS OF GAMES
      KSTUF(5)=0
      IF(.NOT.CROP(AITEM,SHORT)) GO TO 11
      LENGTH=1
      GO TO 13
 11   IF(.NOT.CROP(AITEM,MEDIUM)) GO TO 12
      LENGTH=2
      GO TO 13
 12   IF(.NOT.CROP(AITEM,LONG)) GO TO 20
      LENGTH=4
C
 13   IF(NOMORE()) GO TO 25
 14   CALL SCAN
      IF(KEY .NE. IHALPHA) GO TO 25
C--------CHECK FOR DIFFERENT SKILL LEVELS
      IF(.NOT.CROP(AITEM,NOVICE)) GO TO 15
      SKILL=1
      GO TO 19
 15   IF(.NOT.CROP(AITEM,FAIR)) GO TO 16
      SKILL=2
      GO TO 19
 16   IF(.NOT.CROP(AITEM,GOOD)) GO TO 17
      SKILL=3
      GO TO 19
 17   IF(.NOT.CROP(AITEM,EXPERT)) GO TO 18
      SKILL=4
      GO TO 19
 18   IF(.NOT.CROP(AITEM,EMERITUS)) GO TO 25
      SKILL=5
C
 19   IF(SKILL.EQ.4) KSTUF(5)=1
      IF(SKILL.EQ.5) KSTUF(5)=2
      IF(LENGTH*SKILL .EQ. 0) GO TO 10
      GO TO 30
 20   IF(LENGTH .NE. 0) GO TO 25
      CALL PROMPT(45HWOULD YOU LIKE A SHORT, MEDIUM OR LONG GAME? ,45)
      GO TO 10
 25   IF(SKILL .NE. 0) GO TO 30
      CALL PROMPT(48HARE YOU NOVICE, FAIR, GOOD, EXPERT OR EMERITUS?
     1  ,48)
      GO TO 14
C--------READ IN SECRET PASSWORD
 30   IF(NOMORE()) GO TO 35
 31   PFLAG=.TRUE.
      CALL SCAN
      PFLAG=.FALSE.
      PASSWD=AITEM
      IF(KEY .NE. IHEOL) GO TO 40
 35   CALL PROMPT(33HPLEASE TYPE IN A SECRET PASSWORD:,33)
      GO TO 31
 40   CONTINUE
C--------USE PARAMETERS TO GENERATE INITIAL VALUES OF THINGS
      DAMFAC=0.50*SKILL
      REMBASE=3.0*RANF(0)+2.0
      INPLAN=5. +6.*RANF(0)
      NROMREM=(2.+RANF(0))*SKILL
      NSCREM=SKILL/3
      REMTIME=7.0*LENGTH
      INTIME=REMTIME
      RATE=(SKILL-2.0*RANF(0)+1.0)*SKILL*0.1 + 0.15
      REMKL=2.0*RATE*INTIME
      INKLING=REMKL
      INCOM=SKILL+0.0625*INKLING*RANF(0)
      INCOM=MIN0(10,INCOM)
      REMCOM=INCOM
      REMRES=(INKLING+4* INCOM        )*INTIME
      INRESOR=REMRES
      IF(INKLING.GT.50) REMBASE=REMBASE+1
      INBASE=REMBASE
      RETURN
C--------PROCESS A TOURNAMENT REQUEST
 100  CALL SCAN
      IF (KEY.EQ.IHREAL) ISEED(1)=ABS(TNUMBER)
      IF (KEY.EQ.IHALPHA) ISEED(1)=ABS(ISEED(1))
      CALL RANSET(ISEED(1))
      THINGX=-1
C--------GO BACK FOR ANYTHING LEFT OUT
      IF (KEY.NE.IHEOL) GO TO 9
      CALL PROMPT(37HTYPE IN NAME OR NUMBER OF TOURNAMENT:   ,37)
      GO TO 100
C--------PROCESS A REQUEST FOR A FROZEN GAME
 200  CALL THAW
C--------MAKE SURE WE GOT A GAME OUT OF THAW
      IF(PASSWD.EQ.0.D0) GO TO 5
      FROZEN = .TRUE.
C--------DESTROY ANY "THINGS" IN FROZEN GAME.
      THINGX=0
      THINGY=0
      DO 210 I=1,10
      DO 210 J=1,10
 210  IF(QUAD(I,J) .EQ. IHQUEST) QUAD(I,J)=IHDOT
C--------RESET PLAQUE STATUS
      ICITE=0
      RETURN
      END
C
      LOGICAL FUNCTION NOMORE()
      COMMON/SCANKHAR/KHAR
      COMMON/CHARCOMN/NULL,BLANK
      CHARACTER KHAR,NULL,BLANK
      NOMORE=.TRUE.
      IF(KHAR .EQ. BLANK) NOMORE=.FALSE.
      RETURN
      END
